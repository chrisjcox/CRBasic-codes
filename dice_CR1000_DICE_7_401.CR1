Public PgmVersion As String = "DICE7_401"
' De-Icing Comparison Experiment (D-ICE)
'
' CR1000 Logger #7
'
' Program author: C. Cox christopher.j.cox@noaa.gov
'
' 5/22/2017  
'
' Updates:
'    5/22-31  : development
'    6/1/2017 : sent to logger for calibration to ESRL roof
'               all lines tested vs. 9.998mV standard
'                 psp_34231_V: 9.997/9.993
'                 psp_18135_V: 9.9937
'                 psp_26818_V: 9.9937
'                 psp_20523_V: 9.990/9.993/9.997
'                 psp_26214_V: 9.990/9.993/9.997
'                 psp_26236_V: 9.997/9.993
'                 psp_26226_V: 9.997/9.993
'                 spp_38172_V: 9.9937
'    6/2/2017 : visual checks, emailed to Jim for upload to loggers mounted on ESRL roof
'    6/5/2017 : add station name & program version
'    6/24/2017: add Hukseflux SR30
'    7/4/2017 : move Hukseflux SR30 to Logger #6 with other modbus devices (removed from here)
'    7/5/2017 : 1 min Averages from FP2 to IEEE4
'                version 101 = Calibration, June 2017
'                version 201 = Deployment, August 2017
'   8/13/2017 : add fantchs to P1 & P2
'               add avg_count: this is the number of scans completed for the 1 min DataTable record 
'   8/15/2017 : add StdDev to thermopiles
'   8/18/2017 : final checks, debugging
'               * version 301 = DICE Campaign, beg. 8/18/2017 *
'   8/25/2017 : more checks, debugging back on Boulder
'               * version 401 = DICE Campaign, beg. 8/xx/2017 *
'
'   This program runs Eppley PSPs and SPP
' ---------------------------------------------------------------------
'     Manuf.  Model     SN      Owner     DiffCh    Note
' ---------------------------------------------------------------------  
'     Eppley     PSP    34231  Eppley     1
'     Eppley     PSP    18135  Eppley     2
'     Eppley     PSP    26818  Eppley     3
'     Eppley     PSP    20523  Eppley     4
'     Eppley     PSP    26214  NCAR       5
'     Eppley     PSP    26236  NCAR       6
'     Eppley     PSP    26226  NCAR       7         THIS IS AN UNPLUGGED SPARE
'     Eppley     SPP    38172  Eppley     8
'     
'                  * Fantachs * 
'     Eppley     PSP    26818             P1
'     Eppley     PSP    18135             P2
' ---------------------------------------------------------------------
'

' =====================================================================
' Declare Public Variables
'
' Logger ...
Public PTemp, batt_volt, avg_count

' Eppley PSPs 
Public psp_34231_mV
Public psp_18135_mV
Public psp_26818_mV
Public psp_20523_mV

' Eppley PSPs from NCAR
Public psp_26214_mV
Public psp_26236_mV
Public psp_26226_mV

' Eppley SPP
Public spp_38172_mV

' Eppley Fantachs
Public psp_26818_fantach
Public psp_18135_fantach

' =====================================================================


' =====================================================================
' Define Data Tables.
DataTable (CR1000_DICE_7,1,-1)	
  DataInterval (0,60,Sec,10)
	
  ' Logger ...
	Average (1,PTemp,IEEE4,False)
  Average (1,batt_volt,IEEE4,False)
  ' This is used to count the # of 1s scans that are included in the
	' 1m average: should be 60, but less if lags cause scan > 1s	
  Totalize (1,avg_count,FP2,False)
  
  ' Eppley PSPs
  Average (1,psp_34231_mV,IEEE4,False)
  StdDev  (1,psp_34231_mV,IEEE4,False)
  Average (1,psp_18135_mV,IEEE4,False) 
  StdDev  (1,psp_18135_mV,IEEE4,False)  
  Average (1,psp_26818_mV,IEEE4,False)
  StdDev  (1,psp_26818_mV,IEEE4,False)  
  Average (1,psp_20523_mV,IEEE4,False)
  StdDev  (1,psp_20523_mV,IEEE4,False) 
  
  ' Epply PSPs from NCAR
  Average (1,psp_26214_mV,IEEE4,False)  
  StdDev  (1,psp_26214_mV,IEEE4,False) 
  Average (1,psp_26236_mV,IEEE4,False)
  StdDev  (1,psp_26236_mV,IEEE4,False) 
  Average (1,psp_26226_mV,IEEE4,False) 
  StdDev  (1,psp_26226_mV,IEEE4,False)
  
  ' Eppley SPP
  Average (1,spp_38172_mV,IEEE4,False)
  StdDev  (1,spp_38172_mV,IEEE4,False) 
   
  ' Eppley Fantachs
  Average (1,psp_26818_fantach,IEEE4,False)
  Average (1,psp_18135_fantach,IEEE4,False)  
        
EndTable
' =====================================================================



' =====================================================================
'Main Program
BeginProg

	Scan (1,Sec,0,0)
	  
	  ' Logger ...
		PanelTemp (PTemp,250)
		Battery (batt_volt)
    ' This is just a counter growing an array of ones that will  =
    ' # of scans per minute (when data table is initiated) 
		avg_count = PTemp*0+1

    ' Eppley PSPs 
    VoltDiff (psp_34231_mV,1,AutoRange,1,True ,0,_60Hz,1.0,0)   
    VoltDiff (psp_18135_mV,1,AutoRange,2,True ,0,_60Hz,1.0,0)  
    VoltDiff (psp_26818_mV,1,AutoRange,3,True ,0,_60Hz,1.0,0)      
    VoltDiff (psp_20523_mV,1,AutoRange,4,True ,0,_60Hz,1.0,0)
    
    ' Eppley PSPs from NCAR
    VoltDiff (psp_26214_mV,1,AutoRange,5,True ,0,_60Hz,1.0,0)        
    VoltDiff (psp_26236_mV,1,AutoRange,6,True ,0,_60Hz,1.0,0)   
    VoltDiff (psp_26226_mV,1,AutoRange,7,True ,0,_60Hz,1.0,0) 

    ' Eppley SPP 38172
    VoltDiff (spp_38172_mV,1,AutoRange,8,True ,0,_60Hz,1.0,0)
     
    ' Eppley fantachs
    PulseCount (psp_26818_fantach,1,1,0,1,1.0,0)
    PulseCount (psp_18135_fantach,1,2,0,1,1.0,0)
               
  CallTable CR1000_DICE_7
	NextScan
	
EndProg

' =====================================================================
